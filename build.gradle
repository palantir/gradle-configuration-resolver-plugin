plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'com.jfrog.bintray' version '1.5'
    id 'com.palantir.configuration-resolver' version '0.0.1'
    id 'com.palantir.git-version' version '0.11.0'
    id 'groovy'
    id 'idea'
    id 'nebula.maven-publish' version '13.0.0'
}

repositories {
    jcenter()
}

group 'com.palantir.configurationresolver'
version gitVersion()
sourceCompatibility = 1.7

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'com.netflix.nebula:nebula-test:7.2.5'
}

pluginBundle {
    website = 'https://github.com/palantir/gradle-configuration-resolver-plugin'
    vcsUrl = 'https://github.com/palantir/gradle-configuration-resolver-plugin'
    description = 'Adds a task that resolves all dependencies in a project.'
    tags = ['dependencies']

    plugins {
        configurationResolver {
            id = 'com.palantir.configuration-resolver'
            displayName = 'Configuration Resolver'
        }
    }
}
publishPlugins.onlyIf {
    versionDetails().isCleanTag
}

// Configure the publishPlugins task
tasks.publish.dependsOn publishPlugins
project.ext.'gradle.publish.key' = System.env["GRADLE_KEY"]
project.ext.'gradle.publish.secret' = System.env["GRADLE_SECRET"]
